plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.k2fsa.sherpa.onnx'
    def compileSdkVer = (project.findProperty('ANDROID_COMPILE_SDK') ?: '33') as Integer
    def targetSdkVer = (project.findProperty('ANDROID_TARGET_SDK') ?: '33') as Integer
    def minSdkVer = (project.findProperty('ANDROID_MIN_SDK') ?: '21') as Integer

    compileSdk compileSdkVer

    if (project.hasProperty('ANDROID_NDK_VERSION')) {
        ndkVersion project.property('ANDROID_NDK_VERSION')
    }

    defaultConfig {
        applicationId "com.k2fsa.sherpa.onnx"
        minSdk minSdkVer
        targetSdk targetSdkVer
        versionCode 20251217
        versionName "1.12.20"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Lifecycle + ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-ktx:1.8.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${KOTLINX_COROUTINES_ANDROID_VERSION}"

    // Offline ASR alternative engine (Vosk). Model is provisioned onto device storage by scripts.
    implementation "com.alphacephei:vosk-android:${VOSK_ANDROID_VERSION}"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}